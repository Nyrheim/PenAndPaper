plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id "maven-publish"
}

group = "net.nyrheim"
version = "1.2.0"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://hub.spigotmc.org/nexus/content/groups/public/" }
    maven { url "https://repo.rpkit.com/repository/maven-public/" }
}

dependencies {
    implementation group: "org.spigotmc", name: "spigot-api", version: "1.14.4-R0.1-SNAPSHOT"
    implementation group: "mysql", name: "mysql-connector-java", version: "8.0.20"
    implementation group: "com.zaxxer", name: "HikariCP", version: "3.4.5"
    implementation group: "com.typesafe", name: "config", version: "1.4.0"
    implementation group: "org.jooq", name: "jooq", version: "3.13.2"
    implementation group: "org.slf4j", name: "slf4j-jdk14", version: "1.7.30"
    implementation group: "org.ehcache", name: "ehcache", version: "3.8.1"

    implementation group: "org.jetbrains", name: "annotations", version: "19.0.0"
    implementation group: "org.eclipse.jetty", name: "jetty-server", version: "9.4.29.v20200521"
    implementation group: "org.eclipse.jetty", name: "jetty-servlet", version: "9.4.29.v20200521"
    implementation group: "org.eclipse.jetty", name: "jetty-webapp", version: "9.4.29.v20200521"
    implementation group: "org.pircbotx", name: "pircbotx", version: "2.1"
    implementation group: "org.jetbrains.kotlin", name: "kotlin-stdlib-jdk8", version: "1.3.72"
    implementation group: "org.jetbrains.kotlin", name: "kotlin-reflect", version: "1.3.72"
    implementation group: "com.rpkit", name: "rpk-core", version: "1.9.0"
    implementation group: "com.rpkit", name: "rpk-core-bukkit", version: "1.9.0"
    implementation group: "com.rpkit", name: "rpk-player-lib-bukkit", version: "1.9.0"
    implementation group: "com.rpkit", name: "rpk-character-lib-bukkit", version: "1.9.0"
    implementation group: "com.rpkit", name: "rpk-skill-lib-bukkit", version: "1.9.0"
    implementation group: "com.rpkit", name: "rpk-class-lib-bukkit", version: "1.9.0"
    implementation group: "com.rpkit", name: "rpk-experience-lib-bukkit", version: "1.9.0"
    implementation group: "com.rpkit", name: "rpk-economy-lib-bukkit", version: "1.9.0"
    implementation group: "com.rpkit", name: "rpk-stat-lib-bukkit", version: "1.9.0"
    implementation group: "com.rpkit", name: "rpk-language-lib-bukkit", version: "1.9.0"
}
import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    filter ReplaceTokens, tokens: [
            "version": version
    ]
}

shadowJar {
    dependencies {
        include(dependency("mysql:mysql-connector-java"))
        include(dependency("com.google.protobuf:protobuf-java"))

        include(dependency("com.zaxxer:HikariCP"))

        include(dependency("org.jooq:jooq"))
        include(dependency("org.reactivestreams:reactive-streams"))
        include(dependency("javax.xml.bind:jaxb-api"))
        include(dependency("javax.activation:javax-activation-api"))

        include(dependency("org.slf4j:slf4j-jdk14"))
        include(dependency("org.slf4j:slf4j-api"))

        include(dependency("org.ehcache:ehcache"))
        include(dependency("org.glassfish.jaxb:jaxb-runtime"))
        include(dependency("org.glassfish.jaxb:txw2"))
        include(dependency("com.sun.istack:istack-commons-runtime"))
        include(dependency("org.jvnet.staxex:stax-ex"))
        include(dependency("com.sun.xml.fastinfoset:FastInfoSet"))
        include(dependency("javax.activation:javax.activation-api"))
    }

    relocate "com.mysql", "net.nyrheim.penandpaper.shadow.com.mysql"
    relocate "com.google", "net.nyrheim.penandpaper.shadow.com.google"
    relocate "google", "net.nyrheim.penandpaper.shadow.google"

    relocate "com.zaxxer", "net.nyrheim.penandpaper.shadow.com.zaxxer"

    relocate "org.jooq", "net.nyrheim.penandpaper.shadow.org.jooq"
    relocate "xsd", "net.nyrheim.penandpaper.shadow.xsd"
    relocate "migrations", "net.nyrheim.penandpaper.shadow.migrations"
    relocate "org.reactivestreams", "net.nyrheim.penandpaper.shadow.org.reactivestreams"
    relocate "javax.xml.bind", "net.nyrheim.penandpaper.shadow.javax.xml.bind"
    relocate "javax.activation", "net.nyrheim.penandpaper.shadow.javax.activation"

    relocate "org.slf4j", "net.nyrheim.penandpaper.shadow.org.slf4j"

    relocate "org.ehcache", "net.nyrheim.penandpaper.shadow.org.ehcache"
    relocate "org.terracotta", "net.nyrheim.penandpaper.shadow.org.terracotta"
    relocate "com.sun.xml.bind", "net.nyrheim.penandpaper.shadow.com.sun.xml.bind"
    relocate "com.sun.xml.txw2", "net.nyrheim.penandpaper.shadow.com.sun.xml.txw2"
    relocate "com.sun.istack", "net.nyrheim.penandpaper.shadow.com.sun.istack"
    relocate "org.jvnet.staxex", "net.nyrheim.penandpaper.shadow.org.jvnet.staxex"
    relocate "com.sun.xml.fastinfoset", "net.nyrheim.penandpaper.shadow.com.sun.xml.fastinfoset"
    relocate "javax.activation", "net.nyrheim.penandpaper.shadow.javax.activation"

    mergeServiceFiles()
}

tasks.build.dependsOn tasks.shadowJar

def githubUsername = ""
def githubToken = ""
def githubPropertiesFile = new File(project.projectDir, "github.properties")
if (githubPropertiesFile.exists()) {
    def githubProperties = new Properties()
    githubProperties.load(new FileInputStream(githubPropertiesFile))
    githubUsername = githubProperties.get("github-username")
    githubToken = githubProperties.get("github-token")
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Nyrheim/PenAndPaper")
            credentials {
                username = githubUsername
                password = githubToken
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
